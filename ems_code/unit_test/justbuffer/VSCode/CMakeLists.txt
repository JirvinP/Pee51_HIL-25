cmake_minimum_required(VERSION 3.21)
project(spiQueue VERSION 0.5 DESCRIPTION "spiQueue without the ST platform" LANGUAGES C CXX)

set(CMAKE_C_STANDARD 17) # REQUIRES CMAKE 3.21+
set(CMAKE_C_STANDARD_REQUIRED ON)
set(CMAKE_CXX_STANDARD 14) # REQUIRED BY GTest
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

include(FetchContent)
find_package(Git REQUIRED)
FetchContent_Declare(googletest GIT_REPOSITORY https://github.com/google/googletest.git GIT_TAG v1.15.2)
set(gtest_force_shared_crt ON CACHE BOOL "" FORCE)
FetchContent_MakeAvailable(googletest)
enable_testing()

add_executable(unittest src/gtest.cc)
target_include_directories(unittest PRIVATE inc)
target_link_libraries(unittest PRIVATE GTest::gtest_main spiQueue spiQueueEvil)
add_custom_command(TARGET unittest COMMAND cppcheck --project=compile_commands.json -iout -i_deps --enable=all PRE_BUILD)

add_library(spiQueue SHARED src/spiQueue.c)
target_include_directories(spiQueue PRIVATE inc)
target_link_libraries(spiQueue PRIVATE)

add_library(spiQueueEvil SHARED src/spiQueueEvil.c)
target_include_directories(spiQueueEvil PRIVATE inc)
target_link_libraries(spiQueueEvil PRIVATE)

include(GoogleTest)
gtest_add_tests(TARGET unittest TEST_LIST gtest_list)
set(valgrindCommand valgrind -s --leak-check=full --show-leak-kinds=all --errors-for-leak-kinds=all --undef-value-errors=no --error-exitcode=1 ./unittest)
foreach(case IN LISTS gtest_list)
  if(case MATCHES ^spiQueueTest.)
    add_test(NAME ${case}_vallgrind COMMAND ${valgrindCommand} --gtest_filter=${case})
  endif()
endforeach()

find_package(Doxygen REQUIRED dot mscgen dia)
if (NOT DOXYGEN_FOUND)
  message(WARNING "Doxygen not found")
else(NOT DOXYGEN_FOUND)
set(DOXYGEN_HTML_OUTPUT ${PROJECT_SOURCE_DIR}/doc/html)
  FetchContent_Declare(doxygen-awesome-css GIT_REPOSITORY https://github.com/jothepro/doxygen-awesome-css.git GIT_TAG v2.3.4)
  FetchContent_MakeAvailable(doxygen-awesome-css)
  FetchContent_GetProperties(doxygen-awesome-css SOURCE_DIR AWESOME_CSS_DIR)
  set(DOXYGEN_HTML_EXTRA_FILES ${AWESOME_CSS_DIR}/doxygen-awesome-darkmode-toggle.js)
  set(DOXYGEN_HTML_EXTRA_STYLESHEET ${AWESOME_CSS_DIR}/doxygen-awesome.css ${AWESOME_CSS_DIR}/doxygen-awesome-sidebar-only.css ${AWESOME_CSS_DIR}/doxygen-awesome-sidebar-only-darkmode-toggle.css)
  set(DOXYGEN_HTML_HEADER ${PROJECT_SOURCE_DIR}/doc/src/header.html)
  set(DOXYGEN_GENERATE_TREEVIEW YES)
  set(DOXYGEN_DISABLE_INDEX NO)
  set(DOXYGEN_FULL_SIDEBAR NO)
  set(DOXYGEN_HTML_COLORSTYLE DARK)
  set(DOXYGEN_DOT_IMAGE_FORMAT svg)
  set(DOXYGEN_DOT_TRANSPARENT YES)
  set(DOXYGEN_EXTRACT_ALL YES)
  set(DOXYGEN_OPTIMIZE_OUTPUT_FOR_C YES)
  doxygen_add_docs(doxygen ${PROJECT_SOURCE_DIR}/src/spiQueue.c ${PROJECT_SOURCE_DIR}/inc/spiQueue.h)
endif(NOT DOXYGEN_FOUND)
